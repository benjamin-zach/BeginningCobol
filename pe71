IDENTIFICATION DIVISION.
PROGRAM-ID. GadgetStore.

ENVIRONMENT DIVISION.
INPUT-OUTPUT SECTION.
FILE-CONTROL.
	SELECT GadgetStoreData ASSIGN TO "GadgetStock.dat"
		ORGANIZATION IS LINE SEQUENTIAL.

DATA DIVISION.
FILE SECTION.
FD GadgetStoreData.
01 GadgetRecord.
	88 EndOfGadgetStoreData VALUE HIGH-VALUES.
	02 GadgetId	PIC 9(6).
	02 GadgetName	PIC X(30).
	02 QtyInStock	PIC 9(4).
	02 Price	PIC 9(4)V99.

WORKING-STORAGE SECTION.
01 ValueInStock PIC 9(6)V99 VALUE ZERO.
01 TotalValue PIC 9(6)V99 VALUE ZERO.

01 PrnStockValue.
	02 PrnGadgetName	PIC X(30).
	02 Filler		PIC XX VALUE SPACES.
	02 PrnValue		PIC $$$,$$9.99.

01 PrnFinalStockTotal.
	02 FILLER		PIC X(16) VALUE SPACES.
	02 FILLER		PIC X(16) VALUE "Stock Total:".
	02 PrnFinalTotal	PIC $$$,$$9.99.

PROCEDURE DIVISION.
Begin.
	OPEN INPUT GadgetStoreData
	READ GadgetStoreData
		AT END SET EndOfGadgetStoreData TO TRUE
	END-READ
	PERFORM DisplayGadgetValues UNTIL EndOfGadgetStoreData 
	MOVE TotalValue TO PrnFinalTotal
	DISPLAY PrnFinalStockTotal
	CLOSE GadgetStoreData
	STOP RUN.

DisplayGadgetValues.
	COMPUTE ValueInStock = Price * QtyInStock
	ADD ValueInStock TO TotalValue
	MOVE GadgetName TO PrnGadgetName
	MOVE ValueInStock TO PrnValue
	DISPLAY PrnStockValue
	READ GadgetStoreData
		AT END SET EndOfGadgetStoreData TO TRUE
	END-READ.
